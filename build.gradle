plugins {
  id 'java-library'
//  id "com.github.johnrengelman.shadow" version "${shadowPluginVersion}"
  id 'com.diffplug.spotless' version "${spotlessPluginVersion}"
  id 'net.ltgt.errorprone' version "${errorPronePluginVersion}"
//  id 'checkstyle'
  id 'maven-publish'
  id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    mavenLocal()
}

spotless {
  java {
    toggleOffOn('fmt:off', 'fmt:on')
    removeUnusedImports()
    importOrder()
    eclipse().configFile("${rootDir}/formatter.xml")    
  }
}    

dependencies {

  annotationProcessor 'org.immutables:value:2.9.3'
  compileOnly 'org.immutables:value:2.9.3'
  compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
  
  api "org.slf4j:slf4j-api:${slf4jVersion}"
  api "org.apache.commons:commons-lang3:${commonsLangVersion}"
  api "org.apache.commons:commons-collections4:${commonsCollectionsVersion}"
  api "commons-dbutils:commons-dbutils:${dbUtilsVersion}"
  implementation "dev.walgo:walib:0.7.0"
//  implementation files("/home/walery/dev/dev.walgo/walib/build/libs/walib-0.7.0.jar")

  errorprone "com.google.errorprone:error_prone_core:${errorProneVersion}"

  annotationProcessor "com.google.auto.service:auto-service:${autoServiceVersion}"
  compileOnly "com.google.auto.service:auto-service:${autoServiceVersion}"
  testCompileOnly "com.google.auto.service:auto-service:${autoServiceVersion}"
  
  compileOnly "org.postgresql:postgresql:${postgreSQLDriverVersion}"


  testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
  testImplementation "org.assertj:assertj-core:${assertjVersion}"
// https://mvnrepository.com/artifact/org.hsqldb/hsqldb
  testImplementation "org.hsqldb:hsqldb:${hsqldbVersion}"
// https://mvnrepository.com/artifact/org.hsqldb/sqltool
  testImplementation "org.hsqldb:sqltool:${hsqldbVersion}"

  testImplementation "ch.qos.logback:logback-classic:${logbackVersion}"
  testImplementation "ch.qos.logback:logback-core:${logbackVersion}"
  testImplementation "org.testcontainers:postgresql:${testcontainersPostgresVersion}"
  testImplementation "org.postgresql:postgresql:${postgreSQLDriverVersion}"



}

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters' << "-Xlint:unchecked" << "-Xlint:deprecation"
  dependsOn 'spotlessApply'
  options.errorprone {
      disableWarningsInGeneratedCode = true
      ignoreUnknownCheckNames = true
      allErrorsAsWarnings = true
      // workaround for: https://github.com/google/error-prone/issues/780
//      errorproneArgs = ['-Xep:ParameterName:OFF',
//                        '-Xep:FallThrough:OFF',
//                        '-Xep:UnusedVariable:OFF',
//                        '-Xep:UnusedMethod:OFF',
//                        '-Xep:FunctionalInterfaceClash:OFF',
//                        '-Xep:AssertionFailureIgnored:OFF',
//                        '-Xlint:unchecked',
//                        '-Xlint:deprecation'
//      ]
    }  
} 

compileTestJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters' << "-Xlint:unchecked" << "-Xlint:deprecation"
} 


def defaultManifest(def defaultTitle = null, def defaultVersion = null) {
  ['Implementation-Title': defaultTitle != null ? defaultTitle : title,
   'Implementation-Version': defaultVersion != null ? defaultVersion : version,
   'Implementation-Time': new Date().format("yyyy-MM-dd HH:mm:ss"),
   'Built-By': System.getProperty('user.name'),
   'Built-JDK': System.getProperty('java.version')
  ]
}

test {
  useJUnitPlatform()
  testLogging.showStandardStreams = true
  minHeapSize = "512m"
  maxHeapSize = "1024m"

// listen to events in the test execution lifecycle
  beforeTest { descriptor ->
    logger.lifecycle("Running test: " + descriptor)
  }

// listen to standard out and standard error of the test JVM(s)
//    onOutput { descriptor, event ->
//      logger.lifecycle(descriptor + ": " + event.message)
//    }

  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
  }
  afterSuite { desc, result ->
    if (!desc.parent) { // will match the outermost suite
      println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
    }
  }
}


def pomContent = {
  name "${project.group}:${project.name}"
  description project.description
  url = 'https://github.com/wwalery/DBSeeder'
  groupId project.group
  artifactId project.name
  version project.version
  inceptionYear '2023'
  packaging 'jar'
  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
    }
  }
  developers {
    developer {
      id = 'walery'
      name = 'Walery Wysotsky'
      email = 'dev@wysotsky.info'
    }
  }
  scm {
    connection = 'scm:git:git://github.com:wwalery/DBSeeder.git'
    developerConnection = 'scm:git:ssh://github.com:wwalery/DBSeeder.git'
    url = 'https://github.com/wwalery/DBSeeder/tree/master'
  }
}


task pom {
  doLast {
    pom { project pomContent }.writeTo("pom.xml")
  }
}

jar {
  manifest.attributes.putAll([
    'Implementation-Title': title,
    'Implementation-Version': version,
    'Implementation-Time': new Date().format("yyyy-MM-dd HH:mm:ss"),
    'Built-By': System.getProperty('user.name'),
    'Built-JDK': System.getProperty('java.version')
  ])
}

task javadocJar(type: Jar) {
  archiveClassifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign publishing.publications
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        
            pom {
                packaging = "jar"
                name.set("DBSeeder")
                url.set("https://github.com/wwalery/DBSeeder")
                description.set("Flexible DTO Java classes generator. Generate classes from database metadata (schema)")
            
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection.set("scm:https://github.com/wwalery/DBSeeder.git")
                    developerConnection.set("scm:git@github.com:wwalery/DBSeeder.git")
                    url.set("https://github.com/wwalery/DBSeeder")
                }
                developers {
                    developer {
                        id = 'wwalery'
                        name = 'Valerii Vysotskyi'
                        email = 'dev@wysotsky.info'
                    }
                }
            }

        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}
